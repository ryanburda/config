#!/bin/zsh
#
# NAME
#   theme_picker - synchonizes alacritty and neovim themes.
#
# SYNOPSIS
#   theme_picker
#
# DESCRIPTION
#   Use fzf to select a theme. A theme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzf and the values are strings each
#   containing:
#       * the alacritty theme
#       * the neovim colorscheme
#       * and neovim background
#   in that order separated by spaces.
typeset -A themes=(
    ['Deep-White']="Atelierforest.light deepwhite light"
    ['Everforest-dark']="Eighties.dark everforest dark"
    ['Everforest-light']="Solarized-Light everforest light"
    ['Gruvbox-dark']="Gruvbox-Dark gruvbox-material dark"
    ['Gruvbox-light']="Gruvbox-Light gruvbox-material light"
    ['Kanagawa-dragon']="Base16-Default-Dark kanagawa-dragon dark"
    ['Kanagawa-lotus']="Gruvbox-Light kanagawa-lotus light"
    ['Kanagawa-wave']="Kanagawa kanagawa-wave dark"
    ['Nightfox-carbon']="Astromouse carbonfox dark"
    ['Nightfox-dawn']="Railscasts.light dawnfox light"
    ['Nightfox-day']="Railscasts.light dayfox light"
    ['Nightfox-night']="Kasugano nightfox dark"
    ['Nightfox-nord']="Nord nordfox dark"
    ['Nightfox-tera']="Jasonwryan terafox dark"
)

function set_theme(){
    # accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    theme=${themes[$theme_key]}

    if [[ ! -z $theme ]]; then
        a=$(print $theme | awk -F ' ' '{print $1}')
        b=$(print $theme | awk -F ' ' '{print $2}')
        c=$(print $theme | awk -F ' ' '{print $3}')

        alacritty-themes $a > /dev/null

        echo "$b" > $XDG_CONFIG_HOME/nvim/.colorscheme
        echo "$c" > $XDG_CONFIG_HOME/nvim/.background

        if [[ $c == 'light' ]]; then
            osascript -e 'tell app "System Events" to tell appearance preferences to set dark mode to false'
        else
            osascript -e 'tell app "System Events" to tell appearance preferences to set dark mode to true'
        fi

        echo "$theme_key" > $XDG_CONFIG_HOME/nvim/.theme
    fi
}

current_theme_key=$(<$XDG_CONFIG_HOME/nvim/.theme)
theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | awk -F '\n' -v cur="$current_theme_key" '{if($0==cur) print "ï€Œ " $0; else print $0}' | fzf --cycle)

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
