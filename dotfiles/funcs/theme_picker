#!/bin/zsh
#
# NAME
#   theme_picker - synchonizes alacritty and neovim themes.
#
# SYNOPSIS
#   theme_picker
#
# DESCRIPTION
#   Use fzf to select a theme. A theme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzf and the values are strings each
#   containing:
#       * the alacritty theme
#       * the neovim colorscheme
#       * and neovim background
#   in that order separated by spaces.
typeset -A themes=(
    ['Deep-White']="deepwhite deepwhite light"
    ['Everforest-dark']="everforest_dark everforest dark"
    ['Everforest-light']="everforest_light everforest light"
    ['Gruvbox-dark']="gruvbox_material_medium_dark gruvbox-material dark"
    ['Gruvbox-light']="gruvbox_material_medium_light gruvbox-material light"
    ['Kanagawa-dragon']="kanagawa_dragon kanagawa-dragon dark"
    ['Kanagawa-wave']="kanagawa_wave kanagawa-wave dark"
    ['Nightfox-carbon']="carbonfox carbonfox dark"
    ['Nightfox-dawn']="dawnfox dawnfox light"
    ['Nightfox-day']="dayfox dayfox light"
    ['Nightfox-night']="nightfox nightfox dark"
    ['Nightfox-nord']="nordfox nordfox dark"
    ['Nightfox-tera']="terafox terafox dark"
    ['VSCode']="vscode vscode light"
)

function set_theme(){
    # accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    values=${themes[$theme_key]}

    if [[ ! -z $values ]]; then
        a=$(print $values | awk -F ' ' '{print $1}')
        b=$(print $values | awk -F ' ' '{print $2}')
        c=$(print $values | awk -F ' ' '{print $3}')

        echo "$a" > $XDG_CONFIG_HOME/alacritty/.theme
        echo "$b" > $XDG_CONFIG_HOME/nvim/.colorscheme
        echo "$c" > $XDG_CONFIG_HOME/nvim/.background

        if [[ $c == 'light' ]]; then
            osascript -e 'tell app "System Events" to tell appearance preferences to set dark mode to false'
        else
            osascript -e 'tell app "System Events" to tell appearance preferences to set dark mode to true'
        fi
        
        ~/.zsh/funcs/alacritty_config
        echo "$theme_key" > $XDG_CONFIG_HOME/alacritty/.theme_key

        # aichat theme
        ~/.zsh/funcs/aichat_config
    fi
}

current_theme_key=$(<$XDG_CONFIG_HOME/alacritty/.theme_key)
theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | awk -F '\n' -v cur="$current_theme_key" '{if($0==cur) print "ï€Œ " $0; else print $0}' | fzf --cycle)

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
