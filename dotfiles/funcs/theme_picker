#!/bin/zsh
#
# NAME
#   theme_picker - synchonizes alacritty and neovim themes.
#
# SYNOPSIS
#   theme_picker
#
# DESCRIPTION
#   Use fzf to select a theme. A theme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzf and the values are strings each
#   containing:
#       * the alacritty theme
#       * the neovim colorscheme
#       * and neovim background
#   in that order separated by spaces.
typeset -A themes=(
    ['Dark - Bamboo']="bamboo bamboo dark"
    ['Dark - Everforest']="everforest_dark everforest dark"
    ['Dark - Gruvbox']="gruvbox_material_medium_dark gruvbox-material dark"
    ['Dark - Kanagawa-paper']="kanagawa_wave kanagawa-paper dark"
    ['Dark - Kanagawa-wave']="kanagawa_wave kanagawa-wave dark"
    ['Dark - Nightfox-carbon']="carbonfox carbonfox dark"
    ['Dark - Nightfox-night']="nightfox nightfox dark"
    ['Dark - Nightfox-nord']="nordfox nordfox dark"
    ['Dark - Nightfox-tera']="terafox terafox dark"
    ['Dark - VSCode']="vscode_dark vscode dark"
    ['Dark - ZenBones-forest']="everforest_dark forestbones dark"
    ['Dark - ZenBones-neo']="neobones neobones dark"
    ['Light - Deep-White']="deepwhite deepwhite light"
    ['Light - Everforest']="everforest_light everforest light"
    ['Light - Gruvbox']="gruvbox_material_medium_light gruvbox-material light"
    ['Light - Newpaper']="newpaper newpaper light"
    ['Light - Nightfox-dawn']="dawnfox dawnfox light"
    ['Light - Nightfox-day']="dayfox dayfox light"
    ['Light - VSCode']="vscode_light vscode light"
    ['Light - ZenBones-rose']="zenbones_rose rosebones light"
)

function set_theme(){
    # accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    values=${themes[$theme_key]}

    if [[ ! -z $values ]]; then
        a=$(print $values | awk '{print $1}')
        b=$(print $values | awk '{print $2}')
        c=$(print $values | awk '{print $3}')

        # Write the values to files.
        echo "$a" > $XDG_CONFIG_HOME/alacritty/.theme
        echo "$b" > $XDG_CONFIG_HOME/nvim/.colorscheme
        echo "$c" > $XDG_CONFIG_HOME/nvim/.background
        echo "$theme_key" > $XDG_CONFIG_HOME/alacritty/.theme_key

        # Regenerate templated configs.
        ~/.zsh/funcs/alacritty_config
        ~/.zsh/funcs/aichat_config
        ~/.zsh/funcs/git_config
        ~/.zsh/funcs/lazygit_config
        ~/.zsh/funcs/macos_appearance $(if [[ $c == 'light' ]]; then echo '-l'; fi)
    fi
}

current_theme_key=$(<$XDG_CONFIG_HOME/alacritty/.theme_key)
theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | awk -F '\n' -v cur="$current_theme_key" '{if($0==cur) print "ï€Œ " $0; else print $0}' | fzf --cycle)

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
