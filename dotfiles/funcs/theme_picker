#!/bin/zsh
#
# NAME
#   theme_picker - synchonizes alacritty and neovim themes.
#
# SYNOPSIS
#   theme_picker
#
# DESCRIPTION
#   Use fzy to select a theme. A theme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzy and the values are strings each
#   containing:
#       * the alacritty theme
#       * the neovim colorscheme
#       * and neovim background
#   in that order separated by spaces.

typeset -A themes=(
    ['Kanagawa-wave']="Kanagawa kanagawa-wave dark"
    ['Kanagawa-dragon']="Base16-Default-Dark kanagawa-dragon dark"
    ['Catppuccin-macchiato']="Catppuccin catppuccin-macchiato dark"
    ['Catppuccin-mocha']="Catppuccin catppuccin-mocha dark"
    ['Gruvbox-dark']="Gruvbox-Dark gruvbox dark"
    ['Gruvbox-light']="Gruvbox-Light gruvbox light"
    ['Rose-pine-light']="Mocha.light rose-pine light"
    ['Rose-pine-dark']="Kanagawa rose-pine dark"
    ['Tokyonight-storm']="Tokyonight_Storm tokyonight-storm dark"
    ['Tokyonight-night']="Tokyonight_Night tokyonight-night dark"
    ['Tokyonight-day']="Tokyonight_Day tokyonight-day light"
    ['Nightfox']="Iceberg-Dark nightfox dark"
    ['Dayfox']="Mocha.light dayfox light"
    ['Dawnfox']="Mocha.light dawnfox light"
    ['Tender']="tender tender dark"
)

function set_theme(){
    # accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    theme=${themes[$theme_key]}

    if [[ ! -z $theme ]]; then
        a=$(print $theme | awk -F ' ' '{print $1}')
        b=$(print $theme | awk -F ' ' '{print $2}')
        c=$(print $theme | awk -F ' ' '{print $3}')

        alacritty-themes $a

        echo "$b" > $XDG_CONFIG_HOME/nvim/.colorscheme
        echo "$c" > $XDG_CONFIG_HOME/nvim/.background
    fi
}

theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | fzy -l 20)

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
