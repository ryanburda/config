#!/bin/zsh
#
# NAME
#   colorscheme_picker - synchonizes neovim and terminal colorschemes.
#
# DESCRIPTION
#   Use fzf to select a colorscheme. A colorscheme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzf and the values are strings each
#   containing:
#       * the neovim colorscheme
#       * the neovim background
#       * the wezterm colorscheme
#   in that order separated by commas.
#
#   The colorscheme changes happen as a result of either writing to files or from regeneration of config files:
#       - nvim is updated when a change is detected in $XDG_CONFIG_HOME/nvim/.colorscheme
#       - wezterm is updated by touching $XDG_CONFIG_HOME/wezterm/wezterm.lua
#       - other scripts exist for handling colorscheme changes to other CLIs such as aichat and lazygit.
typeset -A themes=(
    ['Dark - Adwaita']="adwaita,dark,Modus Vivendi (Gogh)"
    ['Dark - Bamboo']="bamboo,dark,Bamboo"
    ['Dark - Cattppuccin']="catppuccin,dark,catppuccin"
    ['Dark - Everforest']="everforest,dark,Everforest Dark (Gogh)"
    ['Dark - Gruvbox']="gruvbox-material,dark,Gruvbox Material (Gogh)"
    ['Dark - Kanagawa-paper']="kanagawa-paper,dark,Kanagawa (Gogh)"
    ['Dark - Kanagawa-wave']="kanagawa-wave,dark,Kanagawa (Gogh)"
    ['Dark - Lackluster']="lackluster,dark,carbonfox"
    ['Dark - Nightfox-carbon']="carbonfox,dark,carbonfox"
    ['Dark - Nightfox-night']="nightfox,dark,nightfox"
    ['Dark - Nightfox-tera']="terafox,dark,terafox"
    ['Dark - Nord']="nordfox,dark,nord"
    ['Dark - VSCode']="vscode,dark,Vs Code Dark+ (Gogh)"
    ['Dark - ZenBones-forest']="forestbones,dark,Everforest Dark (Gogh)"
    ['Dark - ZenBones-neo']="neobones,dark,neobones_dark"
    ['Light - Adwaita']="adwaita,light,Modus Operandi (Gogh)"
    ['Light - Cattppuccin']="catppuccin-latte,light,catppuccin-latte"
    ['Light - Deep-White']="deepwhite,light,Alabaster"
    ['Light - Everforest']="everforest,light,Everforest Light (Gogh)"
    ['Light - Gruvbox']="gruvbox-material,light,GruvboxLight"
    ['Light - Newpaper']="newpaper,light,PaperColor Light (base16)"
    ['Light - Nightfox-dawn']="dawnfox,light,dawnfox"
    ['Light - Nightfox-day']="dayfox,light,dayfox"
    ['Light - VSCode']="vscode,light,Vs Code Light+ (Gogh)"
    ['Light - ZenBones-rose']="rosebones,light,zenbones"
)

function set_theme(){
    # Accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    values=${themes[$theme_key]}

    if [[ ! -z $values ]]; then
        a=$(print $values | awk -F, '{print $1}')
        b=$(print $values | awk -F, '{print $2}')
        c=$(print $values | awk -F, '{print $3}')

        # Write the values to files.
        echo "$a" > $XDG_CONFIG_HOME/nvim/.colorscheme
        echo "$b" > $XDG_CONFIG_HOME/nvim/.background
        echo "$c" > $XDG_CONFIG_HOME/wezterm/.colorscheme

        # This is used below to show a check mark next to the current theme when selecting via fzf
        echo "$theme_key" > $XDG_CONFIG_HOME/wezterm/.colorscheme_key

        # Regenerate templated configs.
        touch $XDG_CONFIG_HOME/wezterm/wezterm.lua
        ~/.zsh/funcs/aichat_config
        ~/.zsh/funcs/git_config
        ~/.zsh/funcs/lazygit_config
        ~/.zsh/funcs/dark_mode $(if [[ $b == 'light' ]]; then echo '-l'; fi)
    fi
}

current_theme_key=$(<$XDG_CONFIG_HOME/wezterm/.colorscheme_key)
theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | fzf --cycle)

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
