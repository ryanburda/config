#!/bin/zsh

# NAME
#   set_colorscheme - synchonizes neovim and terminal colorschemes.
#
# DESCRIPTION
#   Use fzf to select a colorscheme. A colorscheme is applied to both the terminal and neovim.
#   This script utilizes a hard coded associative list (dictionary) where the keys
#   are unique theme names that will show up in fzf and the values are strings each
#   containing:
#       * the neovim colorscheme
#       * the neovim background
#       * the wezterm colorscheme
#   in that order separated by commas.
#
#   The colorscheme changes happen as a result of either writing to files or from regeneration of config files:
#       - nvim is updated when a change is detected in $XDG_CONFIG_HOME/nvim/.colorscheme
#       - wezterm is updated by touching $XDG_CONFIG_HOME/wezterm/wezterm.lua
#       - other scripts exist for handling colorscheme changes to other CLIs such as aichat and lazygit.

: ${XDG_CONFIG_HOME:=$HOME/.config}

typeset -A themes=(
    ['Dark - Catppuccin']="catppuccin,dark,Catppuccin Mocha"
    ['Dark - Everforest']="everforest,dark,Everforest Dark (Gogh)"
    ['Dark - Darkearth']="darkearth,dark,Gruvbox Material (Gogh)"
    ['Dark - Gruvbox']="gruvbox-material,dark,Gruvbox Material (Gogh)"
    ['Dark - Nightfox-dusk']="duskfox,dark,duskfox"
    ['Dark - Nightfox-night']="nightfox,dark,nightfox"
    ['Dark - Nightfox-tera']="terafox,dark,terafox"
    ['Dark - Rose-pine']="rose-pine-moon,dark,rose-pine-moon"
    ['Dark - Vague']="vague,dark,Vacuous 2 (terminal.sexy)"
    ['Light - Catppuccin']="catppuccin,light,Catppuccin Latte"
    ['Light - Everforest']="everforest,light,Everforest Light (Gogh)"
    ['Light - Gruvbox']="gruvbox-material,light,GruvboxLight"
    ['Light - Nightfox-dawn']="dawnfox,light,dawnfox"
    ['Light - Nightfox-day']="dayfox,light,dayfox"
    ['Light - Rose-pine']="rose-pine-dawn,light,rose-pine-dawn"
)

set_theme(){
    # Accepts a single parameter that is a valid key into the themes dictionary above.
    theme_key=$1
    values=${themes[$theme_key]}

    if [[ ! -z $values ]]; then
        nvim_colorscheme=$(print $values | awk -F ',' '{print $1}')
        nvim_background=$(print $values | awk -F ',' '{print $2}')
        wezterm_colorscheme=$(print $values | awk -F ',' '{print $3}')

        envy set colorscheme_key $theme_key
        envy set nvim_colorscheme $nvim_colorscheme
        envy set nvim_background $nvim_background
        envy set wezterm_colorscheme $wezterm_colorscheme

        # Update all running nvim instances
        # On Linux, use XDG_RUNTIME_DIR; on macOS, use TMPDIR or /tmp
        local nvim_runtime_dir="${XDG_RUNTIME_DIR:-${TMPDIR:-/tmp}}"
        # Try both macOS pattern (nvim.*/*/nvim.*.0) and Linux pattern (nvim.*.0)
        # (N) qualifier prevents zsh error when pattern doesn't match
        for socket in "$nvim_runtime_dir"/nvim.*/*/nvim.*.0(N) "$nvim_runtime_dir"/nvim.*.0(N); do
            if [[ -S "$socket" ]]; then
                # redirect stderr to /dev/null since stale socket files can remain even after Neovim instances have closed.
                nvim --server "$socket" --remote-send "<Cmd>luafile ${XDG_CONFIG_HOME}/nvim/lua/colorscheme.lua<CR>" 2>/dev/null
            fi
        done

        # Regenerate templated configs.
        touch $XDG_CONFIG_HOME/wezterm/wezterm.lua
        regenerate_git_config
        regenerate_lazygit_config
        dark_mode $(if [[ $(envy get nvim_background) == 'light' ]]; then echo '-l'; fi)
    fi
}

theme_key=$(printf "%s\n" "${(k)themes[@]}" | sort | fzf --cycle --header "$(envy get colorscheme_key)")

if [[ ! -z $theme_key ]]; then
    set_theme $theme_key
fi
